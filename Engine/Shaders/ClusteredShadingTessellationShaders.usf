// Copyright 1998-2015 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	ClusteredShadingTessellationShaders.usf: domain and hull shaders
=============================================================================*/

#include "ClusteredShadingVertexCommon.usf"


FClusteredShadingBasePassVSToDS PassInterpolate(FClusteredShadingBasePassVSToDS a, float aInterp, FClusteredShadingBasePassVSToDS b, float bInterp)
{
	FClusteredShadingBasePassVSToDS O;
	
	O.FactoryInterpolants = VertexFactoryInterpolate(a.FactoryInterpolants, aInterp, b.FactoryInterpolants, bInterp);

	#if USE_VERTEX_FOG
		TESSELLATION_INTERPOLATE_MEMBER(BasePassInterpolants.VertexFog);
	#endif

	#if USE_WORLD_POSITION_EXCLUDING_SHADER_OFFSETS
		TESSELLATION_INTERPOLATE_MEMBER(BasePassInterpolants.WorldPositionExcludingWPO);
	#endif

	#if WRITES_VELOCITY_TO_GBUFFER
		TESSELLATION_INTERPOLATE_MEMBER(BasePassInterpolants.VelocityPrevScreenPosition);
		#if WRITES_VELOCITY_TO_GBUFFER_USE_POS_INTERPOLATOR
		TESSELLATION_INTERPOLATE_MEMBER(BasePassInterpolants.VelocityScreenPosition);
		#endif
	#endif

	return O;
}

FClusteredShadingBasePassVSToPS PassFinalizeTessellationOutput(FClusteredShadingBasePassVSToDS Interpolants, float4 WorldPosition, FMaterialTessellationParameters MaterialParameters)
{
	FClusteredShadingBasePassVSToPS O;
	
	O.FactoryInterpolants = VertexFactoryAssignInterpolants(Interpolants.FactoryInterpolants);

	// Copy everything in the shared base class over to the pixel shader input
	(FSharedClusteredShadingBasePassInterpolants)O.BasePassInterpolants = (FSharedClusteredShadingBasePassInterpolants)Interpolants.BasePassInterpolants;

	// Transform position to clip-space
	ISOLATE
	{
		O.Position = mul(WorldPosition, ResolvedView.TranslatedWorldToClip);

#if WRITES_VELOCITY_TO_GBUFFER
		float SkipVelocity = O.BasePassInterpolants.VelocityPrevScreenPosition.w;
		O.BasePassInterpolants.VelocityPrevScreenPosition = mul(float4(Interpolants.BasePassInterpolants.VelocityPrevScreenPosition.xyz, 1), ResolvedView.PrevTranslatedWorldToClip);
		// Move the mask to .z when as we don't need the clip space Z
		O.BasePassInterpolants.VelocityPrevScreenPosition.z = SkipVelocity;
		#if WRITES_VELOCITY_TO_GBUFFER_USE_POS_INTERPOLATOR
		    O.BasePassInterpolants.VelocityScreenPosition = mul(float4(Interpolants.BasePassInterpolants.VelocityScreenPosition.xyz, 1), ResolvedView.TranslatedWorldToClip);
		#endif
#endif
	}
	
	// Calc position-dependent interps
	O.BasePassInterpolants.PixelPosition = WorldPosition;

	#if USE_WORLD_POSITION_EXCLUDING_SHADER_OFFSETS
		O.BasePassInterpolants.PixelPositionExcludingWPO = float4(Interpolants.BasePassInterpolants.WorldPositionExcludingWPO, 1);
	#endif

	return O;
}

// This gets us the MainHull and MainDomain shader definitions
#include "Tessellation.usf"
