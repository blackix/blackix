// Copyright 1998-2016 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	HeightFogCommon.usf: 
=============================================================================*/

static const float FLT_EPSILON = 0.001f;
static const float FLT_EPSILON2 = 0.01f;

// @param WorldPositionRelativeToCamera = WorldPosition - InCameraPosition
half4 GetExponentialHeightFog(float3 WorldPositionRelativeToCamera)
{
	/** FogDensity * exp2(-FogHeightFalloff * (CameraWorldPosition.z - FogHeight)) in x, FogHeightFalloff in y, CosTerminatorAngle in z, StartDistance in w. */
	const float4 ExponentialFogParameters = Frame.ExponentialFogParameters0;
	/** Color to use. */
	const half3 ExponentialFogColor = Frame.ExponentialFogParameters1.xyz;
	const half MinFogOpacity = Frame.ExponentialFogParameters1.w;

	float3 CameraToReceiver = WorldPositionRelativeToCamera;
	float CameraToReceiverLengthSqr = dot( CameraToReceiver, CameraToReceiver );
	float CameraToReceiverLengthInv = rsqrt( CameraToReceiverLengthSqr );
	float CameraToReceiverLength = CameraToReceiverLengthSqr * CameraToReceiverLengthInv;
	half3 CameraToReceiverNormalized = CameraToReceiver * CameraToReceiverLengthInv;

	// Calculate the line integral of the ray from the camera to the receiver position through the fog density function
	// The exponential fog density function is d = GlobalDensity * exp(-HeightFalloff * z)
	float EffectiveZ = (abs(CameraToReceiver.z) > FLT_EPSILON2) ? CameraToReceiver.z : FLT_EPSILON2;
	float Falloff = max( -127.0f, ExponentialFogParameters.y * EffectiveZ );	// if it's lower than -127.0, then exp2() goes crazy in OpenGL's GLSL.
	float ExponentialHeightLineIntegralShared = ExponentialFogParameters.x * (1.0f - exp2(-Falloff) ) / Falloff;
	float ExponentialHeightLineIntegral = ExponentialHeightLineIntegralShared * max(CameraToReceiverLength - ExponentialFogParameters.w, 0.0f);

	half3 DirectionalInscattering = 0;

	if (Frame.ExponentialFogInscatteringLightDirection.w > 0)
	{
		half3 InscatteringColor = Frame.ExponentialFogDirectionalInscatteringColor.xyz;
		half3 InscatteringLightDirection = Frame.ExponentialFogInscatteringLightDirection.xyz;
		half InscatteringExponent = Frame.ExponentialFogDirectionalInscatteringColor.w;
		float InscatteringStartDistance = Frame.ExponentialFogDirectionalInscatteringStartDistance;

		// Setup a cosine lobe around the light direction to approximate inscattering from the directional light off of the ambient haze;
		float InscatteringAmount = pow(saturate(dot(CameraToReceiverNormalized, InscatteringLightDirection.xyz)), InscatteringExponent);

		// Calculate the line integral of the eye ray through the haze, using a special starting distance to limit the inscattering to the distance
		float DirExponentialHeightLineIntegral = ExponentialHeightLineIntegralShared * max(CameraToReceiverLength - InscatteringStartDistance, 0.0f);
		// Calculate the amount of light that made it through the fog using the transmission equation
		half DirectionalInscatteringFogFactor = saturate(exp2(-DirExponentialHeightLineIntegral));
		// Final inscattering from the light
		DirectionalInscattering = InscatteringColor * (InscatteringAmount * (1 - DirectionalInscatteringFogFactor));
	}

	// Calculate the amount of light that made it through the fog using the transmission equation
	half ExpFogFactor = max(saturate(exp2(-ExponentialHeightLineIntegral)), MinFogOpacity);

	return half4((ExponentialFogColor) * (1 - ExpFogFactor) + DirectionalInscattering, ExpFogFactor);
}

half4 CalculateVertexHeightFog(float3 WorldPositionRelativeToCamera)
{
	return GetExponentialHeightFog(WorldPositionRelativeToCamera);
}

